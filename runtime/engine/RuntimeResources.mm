#include "engine/RuntimeResources.hpp"
#include "engine/Checked.hpp"
#include "engine/StartupLog.hpp"
#include "metal/abi/ExecutionGeometry.h"

#import <Foundation/Foundation.h>
#include <CommonCrypto/CommonDigest.h>

#include <array>
#include <limits>
#include <optional>
#include <sstream>
#include <utility>

namespace splash::engine {
namespace {

static_assert(model::ExecutionLimits::maximumBatchWidth ==
              SPLASH_MAXIMUM_BATCH_WIDTH);
static_assert(model::ExecutionLimits::prefillTokenBudget ==
              SPLASH_PREFILL_TOKEN_BUDGET);
static_assert(model::ExecutionLimits::draftQueryRows ==
              SPLASH_DRAFT_QUERY_ROWS);
static_assert(model::ExecutionLimits::draftProposalTokens ==
              SPLASH_DRAFT_PROPOSAL_TOKENS);
static_assert(model::ExecutionLimits::targetVerifyRows ==
              SPLASH_TARGET_VERIFY_ROWS);
static_assert(model::ExecutionLimits::draftContextTokens ==
              SPLASH_DRAFT_SLIDING_WINDOW);
static_assert(model::ExecutionLimits::speculativeScratchTokens ==
              SPLASH_SPECULATIVE_SCRATCH_TOKENS);

std::string errorText(RuntimeResourceStage stage, std::string_view message,
                      std::string_view budgetDescription) {
  std::ostringstream out;
  out << "runtime resource assembly failed [" << runtimeResourceStageName(stage)
      << "]: " << message;
  if (!budgetDescription.empty()) {
    out << '\n' << budgetDescription;
  }
  return out.str();
}

uint8_t hexNibble(char value) {
  if (value >= '0' && value <= '9') {
    return static_cast<uint8_t>(value - '0');
  }
  if (value >= 'a' && value <= 'f') {
    return static_cast<uint8_t>(value - 'a' + 10);
  }
  if (value >= 'A' && value <= 'F') {
    return static_cast<uint8_t>(value - 'A' + 10);
  }
  throw std::invalid_argument("manifest SHA-256 is not hexadecimal");
}

uint64_t mebibytes(uint64_t bytes) noexcept { return bytes / kMiB; }

// The startup admission rule. Deliberately independent of the model size:
// weights are mapped, not copied, so the package never has to fit in
// reclaimable memory at once. Residency is what must fit, and it is checked
// here again at every Metal operation as loading and warmup build it up.
void requireStartupHeadroom(
    const MemoryGovernor::HostAvailableMemoryProvider &hostAvailableMemory,
    uint64_t reserveBytes, MemoryPressure pressure) {
  const std::optional<uint64_t> available = hostAvailableMemory();
  if (!available || *available <= reserveBytes ||
      pressure == MemoryPressure::Critical) {
    std::ostringstream message;
    message << "not enough free memory to start: ";
    if (!available)
      message << "reclaimable host memory cannot be measured";
    else
      message << mebibytes(*available) << " MiB reclaimable, "
              << mebibytes(reserveBytes) << " MiB protected for macOS, system "
              << "pressure " << memoryPressureName(pressure);
    message << "; close memory-heavy applications and retry";
    throw metal::MetalAllocationError(message.str(),
                                      metal::AllocationFailure::HostPressure);
  }
}

std::array<uint8_t, 32> parseSha256(std::string_view value) {
  if (value.size() != 64) {
    throw std::invalid_argument(
        "manifest SHA-256 must contain exactly 64 hex characters");
  }
  std::array<uint8_t, 32> result{};
  for (size_t index = 0; index < result.size(); ++index) {
    result[index] = static_cast<uint8_t>((hexNibble(value[index * 2]) << 4) |
                                         hexNibble(value[index * 2 + 1]));
  }
  return result;
}

std::string sha256(std::string_view value) {
  if (value.size() > std::numeric_limits<CC_LONG>::max()) {
    throw std::overflow_error("runtime cache identity is too large to hash");
  }
  std::array<unsigned char, CC_SHA256_DIGEST_LENGTH> digest{};
  if (!CC_SHA256(value.data(), static_cast<CC_LONG>(value.size()),
                 digest.data())) {
    throw std::runtime_error("runtime cache identity SHA-256 failed");
  }
  return digestHex(digest);
}

std::string
canonicalRuntimeCacheNamespace(const RuntimeCacheIdentity &identity) {
  // Length-prefix the unconstrained strings; every other field has a fixed
  // name and decimal representation. This is the one semantic cache tuple,
  // never a hash of compiler padding or native struct bytes.
  std::ostringstream canonical;
  canonical << "splash.runtime-cache-identity\n"
            << "loaded_model_layout_sha256=" << identity.modelLayoutSha256
            << '\n'
            << "build_id_bytes=" << identity.buildId.size() << '\n'
            << "build_id=" << identity.buildId << '\n'
            << "dtype=" << kv::storageFormatName(identity.kvLayout.format()) << '\n'
            << "page_tokens=" << identity.kvLayout.pageTokens << '\n'
            << "elements_per_scale=" << identity.kvLayout.elementsPerScale
            << '\n'
            << "target_model_sha256="
            << digestHex(identity.kvLayout.modelArtifactSha256) << '\n'
            << "q8_quantization=" << identity.kvLayout.quantization << '\n'
            << "q8_scale_type=" << identity.kvLayout.scaleType << '\n'
            << "q8_key_layout=" << identity.kvLayout.keyLayout << '\n'
            << "q8_value_layout=" << identity.kvLayout.valueLayout << '\n'
            << "q8_attention_layers=" << identity.kvLayout.attentionLayers
            << '\n'
            << "q8_kv_heads=" << identity.kvLayout.kvHeads << '\n'
            << "q8_head_dimension=" << identity.kvLayout.headDimension << '\n'
            << "q8_quantized_minimum=" << identity.kvLayout.quantizedMinimum
            << '\n'
            << "q8_quantized_maximum=" << identity.kvLayout.quantizedMaximum
            << '\n'
            << "q8_bytes_per_layer_page=" << identity.kvLayout.bytesPerLayerPage
            << '\n'
            << "q8_bytes_per_model_page=" << identity.kvLayout.bytesPerModelPage
            << '\n';
  return sha256(canonical.str());
}

} // namespace

void requireLoadedModel(const model::ModelPackage &package) {
  if (!package.targetActualAllocatedBytes() ||
      !package.draft.actualAllocatedBytes ||
      (package.descriptor.hasVision() && !package.vision.actualAllocatedBytes) ||
      package.manifestFingerprintSha256.empty() ||
      package.targetManifestFingerprint().empty()) {
    throw std::invalid_argument(
        "loaded model package has incomplete allocation accounting");
  }
}

std::string_view runtimeResourceStageName(RuntimeResourceStage stage) {
  switch (stage) {
  case RuntimeResourceStage::Configuration:
    return "configuration";
  case RuntimeResourceStage::BackendCreation:
    return "backend_creation";
  case RuntimeResourceStage::CapabilityValidation:
    return "capability_validation";
  case RuntimeResourceStage::ModelLoading:
    return "model_loading";
  case RuntimeResourceStage::MemoryPlanning:
    return "memory_planning";
  case RuntimeResourceStage::StorageAllocation:
    return "storage_allocation";
  }
  return "unknown";
}

RuntimeCacheIdentity
makeRuntimeCacheIdentity(std::string_view combinedManifestSha256,
                         std::string_view targetManifestSha256,
                         std::string_view buildId,
                         kv::Layout targetKvLayout) {
  if (buildId.empty()) {
    throw std::invalid_argument("runtime build id is required");
  }
  if (!targetKvLayout.valid()) {
    throw std::invalid_argument("runtime target KV layout is invalid");
  }
  // Parse both digests even though only the target digest belongs in the
  // physical-page ABI. This rejects malformed combined manifests early.
  std::array<uint8_t, 32> combinedDigest = parseSha256(combinedManifestSha256);
  std::array<uint8_t, 32> targetDigest = parseSha256(targetManifestSha256);
  RuntimeCacheIdentity result;
  result.modelLayoutSha256 = digestHex(combinedDigest);
  result.buildId = buildId;
  result.kvLayout = kv::makeLayoutGuard(targetKvLayout, targetDigest);
  result.namespaceSha256 = canonicalRuntimeCacheNamespace(result);
  result.cacheNamespace.digest = parseSha256(result.namespaceSha256);
  return result;
}

RuntimeResourcesError::RuntimeResourcesError(RuntimeResourceStage stage,
                                             std::string message,
                                             std::string statusJson,
                                             std::string budgetDescription,
                                             RuntimeResourceFailure failure)
    : std::runtime_error(errorText(stage, message, budgetDescription)),
      failure_(failure),
      message_(std::move(message)), statusJson_(std::move(statusJson)),
      budgetDescription_(std::move(budgetDescription)) {}

RuntimeResources::RuntimeResources(
    std::unique_ptr<metal::MetalBackend> backend, model::ModelPackage model,
    ops::ExecutionPlans operators,
    EngineMemoryPlan memoryPlan, model::ModelMemoryPlan modelMemoryPlan,
    RuntimeCacheIdentity cacheIdentity,
    std::unique_ptr<MemoryGovernor> memoryGovernor,
    std::unique_ptr<kv::PageStorage> kvPages,
    std::unique_ptr<model::StateStorage> stateStorage,
    std::unique_ptr<model::KvPageTier> kvTier,
    std::unique_ptr<KvPool> kvPool, std::unique_ptr<engine::Cache> cache,
    uint32_t maximumImagePatches, std::optional<uint64_t> hostAvailableAtStart)
    : backend_(std::move(backend)), model_(std::move(model)),
      operators_(std::move(operators)),
      memoryPlan_(std::move(memoryPlan)),
      modelMemoryPlan_(std::move(modelMemoryPlan)),
      cacheIdentity_(std::move(cacheIdentity)),
      memoryGovernor_(std::move(memoryGovernor)), kvPages_(std::move(kvPages)),
      stateStorage_(std::move(stateStorage)), kvTier_(std::move(kvTier)),
      kvPool_(std::move(kvPool)),
      cache_(std::move(cache)), maximumImagePatches_(maximumImagePatches),
      hostAvailableAtStart_(hostAvailableAtStart) {}

std::unique_ptr<RuntimeResources>
RuntimeResources::create(const RuntimeResourcesConfig &config) {
  if (config.metallibPath.empty() || config.modelRoot.empty() ||
      !kv::validFormat(config.kvFormat) ||
      !config.model.valid() ||
      config.buildId.empty() || !config.maximumImagePatches ||
      config.maximumImagePatches % 4) {
    throw RuntimeResourcesError(
        RuntimeResourceStage::Configuration,
        "metallib path, model root, build id, and a merge-aligned image "
        "patch limit are required");
  }
  std::unique_ptr<metal::MetalBackend> backend;
  try {
    backend = std::make_unique<metal::MetalBackend>(
        config.metallibPath.string(), 120.0, 30000u,
        config.residencyKeepAliveSeconds);
  } catch (const metal::MetalAllocationError &error) {
    throw RuntimeResourcesError(RuntimeResourceStage::BackendCreation,
                                error.what(), {}, {},
                                resourceAllocationFailure(error.failure()));
  } catch (const std::exception &error) {
    throw RuntimeResourcesError(RuntimeResourceStage::BackendCreation,
                                error.what());
  }

  const DeviceCapabilities &device = backend->capabilities();
  if (auto error = device.validationError()) {
    throw RuntimeResourcesError(RuntimeResourceStage::CapabilityValidation,
                                *error, deviceStatusJson(device));
  }

  const uint64_t hostReserveBytes =
      EngineMemoryPolicy::hostAvailableReserveBytes(device.physicalMemoryBytes);
  const uint64_t preparationReserveBytes =
      hostReserveBytes + model::kWeightPreparationWorkspaceBytes;
  MemoryGovernor::HostAvailableMemoryProvider hostAvailableMemory =
      config.hostAvailableMemory ? config.hostAvailableMemory
                                 : queryHostAvailableMemory;
  // What other applications leave, measured before the engine takes any.
  const std::optional<uint64_t> hostAvailableAtStart = hostAvailableMemory();
  // Startup work stops on cancellation and keeps its reserve of host memory.
  const auto throwIfCancelled = [cancelled = config.cancelled] {
    if (cancelled && cancelled())
      throw metal::MetalBackendError("startup cancelled");
  };
  const auto currentPressure = [pressure = config.memoryPressure] {
    return pressure ? pressure() : MemoryPressure::Normal;
  };
  const auto admitMetalOperation = [throwIfCancelled, currentPressure,
                                    hostAvailableMemory, hostReserveBytes] {
    throwIfCancelled();
    requireStartupHeadroom(hostAvailableMemory, hostReserveBytes,
                           currentPressure());
  };
  const auto admitWeightPreparation = [throwIfCancelled, currentPressure,
                                       hostAvailableMemory,
                                       preparationReserveBytes] {
    throwIfCancelled();
    const MemoryPressure level = currentPressure();
    if (level != MemoryPressure::Normal)
      throw metal::MetalAllocationError(
          "weight preparation requires normal memory pressure",
          metal::AllocationFailure::HostPressure);
    requireStartupHeadroom(hostAvailableMemory, preparationReserveBytes, level);
  };
  backend->setOperationGuard(admitMetalOperation);
  try {
    const uint64_t hardBudgetBytes = EngineMemoryPolicy::hardBudgetBytes(
        device.recommendedMaxWorkingSetBytes, config.maximumMemoryBytes);
    // Reject a model that cannot fit before preparing or registering its
    // weights. Beside them the plan needs at least the runtime reserves, one
    // state cell, one KV extent and any disk tier KV staging; the full plan
    // below adds the arenas.
    kv::Layout kvLayout = config.model.targetKvLayout;
    kvLayout.format = config.kvFormat;
    uint64_t requiredBytes = 0;
    for (const uint64_t bytes :
         {model::preparedModelWeightBytes(config.modelRoot, config.model),
          model::kPipelineReserveBytes, model::kRuntimeOverheadReserveBytes,
          config.model.stateLayout.activeCellBytes(),
          uint64_t{kvLayout.backingExtentPages()} *
              kvLayout.bytesPerModelPage(),
          config.maximumCacheDiskBytes ? model::KvPageTier::stagingBytesFor(kvLayout)
                                       : 0}) {
      if (!checkedAdd(requiredBytes, bytes, requiredBytes))
        requiredBytes = std::numeric_limits<uint64_t>::max();
    }
    if (requiredBytes > hardBudgetBytes) {
      throw RuntimeResourcesError(
          RuntimeResourceStage::MemoryPlanning,
          "model weights with the runtime reserves, one state cell, one KV "
          "extent and any disk tier KV staging require " +
              std::to_string(requiredBytes) +
              " bytes but the Metal memory budget is " +
              std::to_string(hardBudgetBytes) + " bytes",
          deviceStatusJson(device), {}, RuntimeResourceFailure::EngineCapacity);
    }
    // Fail before opening the package when the machine has no headroom at
    // all; the guard installed above keeps checking as residency grows.
    admitMetalOperation();
  } catch (const RuntimeResourcesError &) {
    throw;
  } catch (const metal::MetalAllocationError &error) {
    throw RuntimeResourcesError(RuntimeResourceStage::ModelLoading,
                                error.what(), deviceStatusJson(device), {},
                                resourceAllocationFailure(error.failure()));
  } catch (const std::exception &error) {
    throw RuntimeResourcesError(RuntimeResourceStage::ModelLoading,
                                error.what(), deviceStatusJson(device));
  }

  model::ModelPackage package;
  try {
    package = model::loadModelPackage(*backend, config.modelRoot, config.model,
                                      admitWeightPreparation);
    requireLoadedModel(package);
  } catch (const metal::MetalAllocationError &error) {
    throw RuntimeResourcesError(RuntimeResourceStage::ModelLoading,
                                error.what(), deviceStatusJson(device), {},
                                resourceAllocationFailure(error.failure()));
  } catch (const std::exception &error) {
    throw RuntimeResourcesError(RuntimeResourceStage::ModelLoading,
                                error.what(), deviceStatusJson(device));
  }

  // One selection owner is used both before allocation and during encoding.
  // The engine lends it to model execution without inspecting kernel choices.
  ops::ExecutionPlans operators(device);
  model::ModelMemoryPlan modelMemoryPlan;
  // The disk tier's KV staging is Metal memory the governor charges beside
  // the weights, so the plan sets it aside before it sizes the KV pool.
  const uint64_t kvStagingBytes =
      config.maximumCacheDiskBytes
          ? model::KvPageTier::stagingBytesFor(package.targetKvLayout(config.kvFormat))
          : 0;
  auto prepareMemory = [&]() -> EngineMemoryPlan {
    try {
      modelMemoryPlan = model::plannedRuntimeMemory(device, package, operators, config.kvFormat);
      if (auto error = modelMemoryPlan.validationError()) {
        throw std::invalid_argument(*error);
      }
    } catch (const std::exception &error) {
      throw RuntimeResourcesError(
          RuntimeResourceStage::MemoryPlanning,
          std::string("model allocated-size plan is invalid: ") + error.what(),
          deviceStatusJson(device));
    }

    ModelMemoryFootprint footprint{
        package.targetActualAllocatedBytes(),
        package.draft.actualAllocatedBytes,
        package.vision.actualAllocatedBytes,
        modelMemoryPlan.activeStateCellPlannedAllocatedBytes,
        modelMemoryPlan.sharedPrefillPlannedAllocatedBytes,
        modelMemoryPlan.sharedDecodePlannedAllocatedBytes,
        modelMemoryPlan.pipelineReserveBytes,
        modelMemoryPlan.runtimeOverheadReserveBytes,
        kvStagingBytes,
    };

    ModelMemoryProfile modelProfile{
        package.name(), package.maximumContextTokens(),
        package.targetKvLayout(config.kvFormat), footprint};
    EngineMemoryPlanResult planResult =
        evaluateEngineMemoryPlan(device, modelProfile, config.maximumMemoryBytes);
    if (!planResult.plan) {
      throw RuntimeResourcesError(
          RuntimeResourceStage::MemoryPlanning, planResult.status.message,
          planResult.status.toStatusJson(), planResult.status.describe());
    }
    EngineMemoryPlan memoryPlan = std::move(*planResult.plan);
    return memoryPlan;
  };
  // Establish the serving baseline and the one real memory governor before
  // installing the shipped choices. Selected workspace never gets a separate
  // allowance or replaces the immutable engine-wide ceiling.
  EngineMemoryPlan memoryPlan = prepareMemory();

  RuntimeCacheIdentity cacheIdentity;
  try {
    cacheIdentity = makeRuntimeCacheIdentity(
        package.manifestFingerprintSha256,
        package.targetManifestFingerprint(), config.buildId,
        package.targetKvLayout(config.kvFormat));
  } catch (const std::exception &error) {
    throw RuntimeResourcesError(RuntimeResourceStage::ModelLoading,
                                error.what(), memoryPlan.toStatusJson(),
                                memoryPlan.breakdown().describe());
  }

  try {
    const auto baselineMemoryPlan = memoryPlan;
    const auto baselineModelMemoryPlan = modelMemoryPlan;
    const EngineMemoryBreakdown &baselineBudget = baselineMemoryPlan.breakdown();
    const uint64_t runtimeReserve =
        baselineBudget.pipelineReserveBytes + baselineBudget.runtimeOverheadReserveBytes;
    // The governor holds the complete Metal footprint to the hard budget. The
    // plan budgets pipelines and driver allocations inside the pipeline and
    // allocator reserves, so memory outside the backend's buffers is charged
    // only beyond them, and elastic state and KV never grow into them.
    auto memoryGovernor = std::make_unique<MemoryGovernor>(
        *backend, baselineBudget.hardBudgetBytes, hostReserveBytes,
        hostAvailableMemory, runtimeReserve);
    if (config.memoryPressure)
      memoryGovernor->setPressure(config.memoryPressure());
    std::string rejected;
    auto adoptChoices = [&](const ops::OperatorChoices &choices) {
      try {
        operators.install(choices);
        auto selectedMemoryPlan = prepareMemory();
        const auto &selected = selectedMemoryPlan.breakdown();
        if (selected.pipelineReserveBytes + selected.runtimeOverheadReserveBytes !=
                runtimeReserve || selected.hardBudgetBytes != baselineBudget.hardBudgetBytes)
          throw std::logic_error("operator choices changed the memory governor ceiling");
        memoryPlan = std::move(selectedMemoryPlan);
        return true;
      } catch (const std::exception &error) {
        // An illegal table entry or a host/user limit that no longer fits the
        // selected scratch keeps the operator defaults, never a partial table.
        rejected = error.what();
        operators.install({});
        modelMemoryPlan = baselineModelMemoryPlan;
        memoryPlan = baselineMemoryPlan;
        return false;
      }
    };
    logKernelStartup("Kernel policy for GPU family ", device.appleGpuFamily,
                     " with ", device.gpuCoreCount, " cores.");
    if (config.operatorChoices && !config.operatorChoices->empty()) {
      if (adoptChoices(*config.operatorChoices))
        logKernelStartup("Installed supplied kernel choices.");
      else
        logKernelStartup("Supplied kernel choices rejected (", rejected,
                         "); using the kernel policy.");
    }

    const EngineMemoryBreakdown &budget = memoryPlan.breakdown();
    auto kvPages = std::make_unique<kv::PageStorage>(
        *backend, memoryGovernor->allocationAdmission(), package.targetKvLayout(config.kvFormat),
        budget.kvVirtualPages);
    // One disk quota serves KV pages and states. Without room for a state,
    // disk KV cannot preserve a restorable prefix, so the tier stays off.
    std::shared_ptr<model::DiskBudget> diskBudget;
    std::shared_ptr<model::SlotFile> stateFile;
    const uint64_t stateBytes = package.stateLayout().cachedBytes();
    if (config.maximumCacheDiskBytes) {
      diskBudget = std::make_shared<model::DiskBudget>(config.maximumCacheDiskBytes);
      try {
        stateFile = std::make_shared<model::SlotFile>(stateBytes, diskBudget);
      } catch (const std::exception &error) {
        diskBudget.reset();
        logKernelStartup("Cache disk tier disabled (", error.what(), ").");
      }
    }
    std::unique_ptr<model::StateStorage> stateStorage = model::createStateStorage(
        *backend, memoryGovernor->allocationAdmission(), package, stateFile);
    if (!stateStorage) {
      throw std::runtime_error("model factory returned no state storage");
    }
    std::unique_ptr<model::KvPageTier> kvTier;
    if (diskBudget) {
      try {
        const uint64_t slotBytes = model::KvPageTier::slotBytesFor(*kvPages);
        kvTier = std::make_unique<model::KvPageTier>(
            *backend, *kvPages, std::make_shared<model::SlotFile>(slotBytes, diskBudget));
        logKernelStartup("Cache disk tier: ", config.maximumCacheDiskBytes / kMiB,
                         " MiB for KV pages of ", slotBytes / 1024, " KiB and states of ",
                         stateBytes / kMiB, " MiB; KV pages stage through ",
                         kvStagingBytes / kMiB, " MiB of Metal memory",
                         stateFile ? ", states through host memory." : ".");
      } catch (const std::exception &error) {
        logKernelStartup("Cache disk KV storage disabled; state storage remains enabled (",
                         error.what(), ").");
      }
    }
    auto kvPool = std::make_unique<KvPool>(*kvPages);
    auto cache = std::make_unique<engine::Cache>(*kvPool, cacheIdentity.cacheNamespace,
                                                 kvTier.get(), diskBudget);

    if (kvPages->declaredBytes() != budget.kvVirtualBytes ||
        kvPages->actualAllocatedBytes() > budget.kvVirtualBytes) {
      throw std::runtime_error(
          "actual KV page storage exceeds its planned category");
    }
    if (stateStorage->actualAllocatedBytes() != 0) {
      throw std::runtime_error("state cells were allocated eagerly");
    }
    metal::MetalMemoryStats memory = backend->memoryStats();
    if (!backend->healthy()) {
      throw std::runtime_error(
          "Metal backend became unhealthy during resource allocation: " +
          backend->unhealthyReason());
    }
    if (memory.allocatedBytes > budget.hardBudgetBytes ||
        memory.deviceCurrentAllocatedBytes > budget.hardBudgetBytes) {
      throw std::runtime_error(
          "base Metal allocation exceeds immutable hard budget");
    }

    auto result = std::unique_ptr<RuntimeResources>(new RuntimeResources(
        std::move(backend), std::move(package), std::move(operators),
        std::move(memoryPlan),
        std::move(modelMemoryPlan), std::move(cacheIdentity),
        std::move(memoryGovernor), std::move(kvPages), std::move(stateStorage),
        std::move(kvTier), std::move(kvPool), std::move(cache),
        config.maximumImagePatches, hostAvailableAtStart));
    return result;
  } catch (const metal::MetalAllocationError &error) {
    throw RuntimeResourcesError(RuntimeResourceStage::StorageAllocation,
                                error.what(), memoryPlan.toStatusJson(),
                                memoryPlan.breakdown().describe(),
                                resourceAllocationFailure(error.failure()));
  } catch (const std::exception &error) {
    throw RuntimeResourcesError(RuntimeResourceStage::StorageAllocation,
                                error.what(), memoryPlan.toStatusJson(),
                                memoryPlan.breakdown().describe());
  }
}

model::RuntimeContext RuntimeResources::modelContext() noexcept {
  const EngineMemoryBreakdown &budget = memoryPlan_.breakdown();
  return {
      *backend_,
      memoryGovernor_->allocationAdmission(),
      model_,
      *kvPages_,
      *stateStorage_,
      operators_,
      maximumImagePatches_,
      budget.pipelineReserveBytes,
      budget.runtimeOverheadReserveBytes,
      kvTier_.get(),
  };
}

ActualMemoryReport RuntimeResources::actualMemoryReport(
    const model::ModelMemoryActual &modelMemory,
    uint64_t estimatedWarmupPeakBytes) const {
  ActualMemoryReport report;
  report.targetWeightsBytes = model_.targetActualAllocatedBytes();
  report.draftWeightsBytes = model_.draft.actualAllocatedBytes;
  report.visionWeightsBytes = model_.vision.actualAllocatedBytes;
  report.stateResidentBytes = modelMemory.stateActualAllocatedBytes;
  report.sharedPrefillBytes = modelMemory.sharedPrefillActualAllocatedBytes;
  report.sharedDecodeBytes = modelMemory.sharedDecodeActualAllocatedBytes;
  report.kvResidentBytes = kvPages_->actualAllocatedBytes();
  report.kvStagingBytes = kvTier_ ? kvTier_->actualAllocatedBytes() : 0;
  // Optional warmup may end with a rolled-back allocation and no subsequent
  // command. Refresh current residency after that rollback; peaks stay intact.
  metal::MetalMemoryStats memory = backend_->refreshMemoryStats();
  if (memory.sparseResidentBytes >
      std::numeric_limits<uint64_t>::max() - memory.allocatedBytes) {
    throw std::overflow_error("backend memory accounting overflows");
  }
  report.backendAllocatedBytes =
      memory.allocatedBytes + memory.sparseResidentBytes;
  report.deviceCurrentAllocatedBytes = memory.deviceCurrentAllocatedBytes;
  report.devicePeakAllocatedBytes = memory.devicePeakAllocatedBytes;
  // A capacity-limited warmup can roll back a partial allocation before it
  // returns a result. Preserve that tracked high-water mark independently of
  // the device-wide measurement used by the audit.
  const auto &budget = memoryPlan_.breakdown();
  const uint64_t reserves =
      budget.pipelineReserveBytes + budget.runtimeOverheadReserveBytes;
  if (memory.peakResidentBytes >
      std::numeric_limits<uint64_t>::max() - reserves) {
    throw std::overflow_error("warmup memory estimate overflows");
  }
  report.estimatedWarmupPeakBytes =
      std::max(estimatedWarmupPeakBytes, memory.peakResidentBytes + reserves);
  return report;
}

} // namespace splash::engine
